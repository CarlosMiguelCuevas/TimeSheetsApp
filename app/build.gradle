apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    defaultConfig {
        applicationId "rraya.nearsoft.com.timesheetsapp"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 1
        versionName "1.0"
        resConfigs "en", "es"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        buildConfigField 'String', 'GIPHY_BACKEND', '"https://api.giphy.com"'
        buildConfigField 'String', 'GIPHY_API_KEY', '"dc6zaTOxFJmzC"'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"
    productFlavors {
        local {
            buildConfigField 'String', 'HOST', '"http://10.0.2.2:8000"'
        }
        mock {
            buildConfigField 'String', 'HOST', '"http://10.0.2.2:8000"'
        }
        prod {
            buildConfigField 'String', 'HOST', '"https://nearsoft-invoices.herokuapp.com"'
        }
        stage {
            buildConfigField 'String', 'HOST', '"http://nearsoft-invoices-stage.herokuapp.com"'
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if (variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }
}

dependencies {
    //app
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"
    implementation "com.android.support:design:$supportLibraryVersion"

    //firebase
    implementation "com.google.firebase:firebase-core:$firebaseLibrariesVersion"
    implementation "com.google.firebase:firebase-auth:$firebaseLibrariesVersion"
    implementation "com.firebaseui:firebase-ui-auth:$firebaseUiLibrariesVersion"

    //Dagger2
    implementation "com.google.dagger:dagger:$daggerLibrariesVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerLibrariesVersion"
    implementation "com.google.dagger:dagger-android:$daggerLibrariesVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerLibrariesVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerLibrariesVersion"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$retrofitLibrariesVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitLibrariesVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitLibrariesVersion"

    //Okhttp
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpInterceptorLibrariesVersion"

    //testing
    testImplementation "junit:junit:$junitVersion"
    androidTestImplementation "com.android.support.test:runner:$runnerVersion"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$espressoVersion"

    //RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    //Glide
    implementation "com.github.bumptech.glide:glide:$glideLibraryVersion"
    kapt "com.github.bumptech.glide:compiler:$glideLibraryVersion"
}

apply plugin: 'com.google.gms.google-services'